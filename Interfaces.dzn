extern String $std::string$;
extern int $int$;
extern double $double$;

interface iMotor
{
	in void turnClockwise();
	in void turnOff();
	in void turnCounterClockwise();
	
	enum State {Clock, Off, CounterClock};
	behaviour {
		
		State state = State.Off;
		
		[state.Clock] {
			on turnClockwise: illegal;
			on turnCounterClockwise: illegal;
			on turnOff: state = State.Off;
		}
		
		[state.Off] {
			on turnClockwise: state = State.Clock;
			on turnOff: {}
			on turnCounterClockwise: state = State.CounterClock;
		}
		
		[state.CounterClock] {
			on turnClockwise: illegal;
			on turnCounterClockwise: illegal;
			on turnOff: state = State.Off;
		}
	}
}

interface iOutput {
	in void turnOn();
	in void turnOff();
	in void output(in String output);
	
	behaviour {
		enum State {On, Off};
		State state = State.Off;
		
		[state.Off] {
			on turnOn: state = State.On;
			on turnOff: {}
			on output: illegal;
		}
		
		[state.On] {
			on turnOn: illegal;
			on turnOff: state = State.Off;
			on output: {}
		}
	}
}

interface iSensor {
	in void turnOn();
	in void turnOff();
	in void calibrate();
	out void measures(in int value);
	
	behaviour {
		enum State {On, Off};
		State state = State.Off;
		
		[state.Off] {
			on turnOn: state = State.On; 
			on turnOff: {}
			on calibrate: illegal;
		}
		
		[state.On] {
			on turnOn: illegal;
			on turnOff: state = State.Off;
			on calibrate: {}
			on inevitable: measures;
		}	
	}
	
}

interface iBlackWhiteSensor {
	in void turnOn();
	in void turnOff();
	in void calibrate();
	out void measuresBlack();
	out void measuresWhite();
	out void measuresError();
	
	behaviour {
		enum State {On, Off};
		State state = State.Off;
		
		[state.Off] {
			on turnOn: state = State.On; 
			on turnOff: {}
			on calibrate: illegal;
		}
		
		[state.On] {
			on turnOn: illegal;
			on turnOff: state = State.Off;
			on calibrate: {}
			on inevitable: measuresBlack;
			on inevitable: measuresWhite;
			on inevitable: measuresError;
		}	
	}
	
}

interface iControl {
	enum State {On, Off};
	in void turnOn();
	in void turnOff();
	
	behaviour {
		State state = State.Off;
		
		[state.Off] {
			on turnOn: state = State.On; 
			on turnOff: {}
		}
		[state.On] {
			on turnOn: illegal;
			on turnOff: state = State.Off;
		}
	}
}

interface iConveyerBelt {
	enum State {On, Off};
	enum Direction {Clockwise, CounterClockwise};
	in void turnOn();
	in void turnOff();
	in void setClockwise();
	in void setCounterClockwise();
	
	behaviour {
		State state = State.Off;
		
		[state.Off] {
			on turnOn: state = State.On;
			on turnOff: {}
			on setClockwise: {}
			on setCounterClockwise: {}
		}
		[state.On] {
			on turnOn: illegal;
			on turnOff: state = State.Off;
			on setClockwise: illegal;
			on setCounterClockwise: illegal;
		}
		
	}
	
}

interface CommunicationProtocol {
	in void sortMode();
//	in void errorsResolved();
//	in void startup();
//	in void shutdown();
	
	behaviour {
		on sortMode: {}
	}
}

interface iPusherControl {
	in void enqueueBox1(double time);
	in void enqueueBox2(double time);
	in void enqueueBox3(double time);
	in void enqueueBox4(double time);
	
	behaviour {
		on enqueueBox1: {}
		on enqueueBox2: {}
		on enqueueBox3: {}
		on enqueueBox4: {}
	}
}

interface iPusher {
	in void down();
	in void up();
	in bool isMoving();
	out void stopped();
	
	enum State {Down, Up};
	
	behaviour {
		bool moving = false;
		State movingState = State.Down;
		
		on isMoving: reply(moving); 
		[moving] {
			on inevitable: { stopped; moving = false; }
			on up: {
				[movingState.Up] {}
				[otherwise] illegal;
			}
			on down: {
				[movingState.Down] {}
				[otherwise] illegal;	
			}
		} 
		[!moving] {
			on down: { moving = true; movingState = State.Down; }
			on down: stopped;
			on up: { moving = true; movingState = State.Up; }
			on up: stopped;
		}
	}
}

interface iTimer {
  in void createTimer(double time);
  in void cancelTimer();
  out void timeout();

  behaviour {
    enum State {Idle, Busy};
    State state = State.Idle;

    [state.Idle] {
      on createTimer: {state = State.Busy;}
      on cancelTimer: illegal;
    }
    [state.Busy] {
      on createTimer: illegal;
      on cancelTimer: {state = State.Idle;}
      on inevitable: { timeout; state = State.Idle; }
    }
  }
}
